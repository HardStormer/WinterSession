[[_Интеллектуальные системы и технологии Задачи]]

_Данная тема просто описывает пример решения логической задачи и не предполагает дополнительных заданий._

Попробуем составить на Прологе известную логическую задачу о волке, козе и капусте.

Крестьянину нужно перевезти через реку волка, козу и капусту. Но лодка такова, что в ней может поместиться только крестьянин, а с ним или один волк, или одна коза, или одна капуста. Но если оставить волка с козой, то волк съест козу, а если оставить козу с капустой, то коза съест капусту. Как перевез свой груз крестьянин.

Будем считать, что переправа осуществляется с левого берега на правый. Очевидно, что первым ходом лодочник не может перевезти волка (т.к. коза сразу съест капусту), ни капусту (т.к. волк съест козу), ни переправиться порожняком (интересно, успеет ли коза съесть капусту раньше чем её саму съест волк).

Определим алгоритм действий, который позволит нам найти решения путем проб и ошибок. Для этого определим несколько дополнительных условий.

1. С правого берега на левый желательно плыть порожняком (если это возможно). С левого на правый порожняком плыть бессмысленно.
2. Если мы перевезли нечто с одного берега на другой, также не имеет смысла вести это немедленно обратно.
3. Следует определить приоритет грузов. Например такой: волк, коза, капуста.

Теперь попробуем составить соответствующие предикаты на Прологе.
Для начала введем предикат напротив, чтобы определить смену берегов.

```SWI-Prolog
напротив(правый,левый).  
напротив(левый,правый).
```

Определим предикат состояние (Крестьянин, Волк, Коза, Капуста, Последний), где первые четыре принимают значение левый или правый (в зависимости от того, на каком берегу они находятся), а Последний обозначает последний перевезенный объект (в случае переправки порожняком будем использовать значение "ничего").

В процессе решения будем печатать все получаемые решения, в том числе и неправильные.

Поскольку порядок правил имеет важное значение при проведении вывода, начнем поиск решения с описания итогового состояния.

```SWI-Prolog
состояние(правый,правый,правый,правый,_):-  
write("Переправа завершена"),nl,  
write("Итоговая последовательность действий в обратном порядке:"),nl.  
```
  
Это правило означает, что переправа считается завершенной, если все её участники одновременно оказались на правом берегу.  
Теперь приведем два правила, описывающие ошибочные состояния.  
  
```SWI-Prolog
состояние(Крестьянин,Волк,Коза,_,_):-  
Волк=Коза,not(Коза=Крестьянин),write("Козу съели"),nl,!,fail.  
  
состояние(Крестьянин,_,Коза,Капуста,_):-  
Капуста=Коза,not(Коза=Крестьянин),write("Капусту съели"),nl,!,fail.  
```
  
Первое из состояний описывает ситуацию, когда волк и коза оказались на одной стороне реки, а лодочник на другой. выдаем соответствующее печальное сообщение и явно указываем ложность решения с помощью предиката fail. Второе описывает аналогичную ситуацию с козой и капустой.  
  
```SWI-Prolog
состояние(правый,Волк,Коза,Капуста,Последний):-  
not(Последний=ничего),write("Поехали порожняком на левый"),nl,  
состояние(левый,Волк,Коза,Капуста,ничего).  
```
  
Реализуем дополнительное условие 1. Если лодочник на правом берегу стоит попробовать вернуться порожняком.  
  
```SWI-Prolog
состояние(A,A,Коза,Капуста,Последний):-  
not(Последний=волк),напротив(A,O),  
write("Повезли волка на"),write(O),nl,  
состояние(O,O,Коза,Капуста,волк).  
```
  
Сначала пробуем перевезти волка. это возможно, если крестьянин и волк на одном берегу и при этом крестьянин не привез волка только что.  
  
```SWI-Prolog
состояние(A,Волк,A,Капуста,Последний):-  
not(Последний=коза),напротив(A,O),  
write("Повезли козу на "),write(O),nl,  
состояние(O,Волк,O,Капуста,коза).  
```
  
теперь пробуем перевести козу.  
  
```SWI-Prolog
состояние(A,Волк,Коза,A,Последний):-  
not(Последний=капуста),напротив(A,O),  
write("Повезли капусту на "),write(O),nl,  
состояние(O,Волк,Коза,O,капуста).  
```
  
Наконец капусту.  
  
```SWI-Prolog
напротив(правый,левый).  
напротив(левый,правый).  
задача:-состояние(левый,левый,левый,левый,ничего).  
```

Для простоты создадим предикат задача, описывающий исходное состояние.  
Теперь запустив программу можем посмотреть полученное решение и весь процесс его получения.  
  
Повезли волка на правый  
Капусту съели (первая ошибка волка перевозить нельзя)  
Повезли козу на правый  
Поехали порожняком на левый  
Повезли волка на правый  
Поехали порожняком на левый  
Козу съели (вторая ошибка, нельзя просто уезжать, нужно забрать с собой кого-нибудь. Поскольку волка только-что привезли, забираем козу.)  
Повезли козу на левый  
Повезли капусту на правый  
Поехали порожняком на левый  
Повезли козу на правый  
Переправа завершена

```SWI-Prolog
состояние(правый,правый,правый,правый,_):-  
write("Переправа завершена"),nl.  
  
состояние(Крестьянин,Волк,Коза,_,_):-  
Волк=Коза,not(Коза=Крестьянин),write("Козу съели"),nl,!,fail.  
  
состояние(Крестьянин,_,Коза,Капуста,_):-  
Капуста=Коза,not(Коза=Крестьянин),write("Капусту съели"),nl,!,fail.  
  
  
состояние(правый,Волк,Коза,Капуста,Последний):-  
Последний\==ничего,write("Поехали порожняком на левый"),nl,  
состояние(левый,Волк,Коза,Капуста,ничего).  
  
  
состояние(Берег1,Берег1,Коза,Капуста,Последний):-  
Последний\==волк,напротив(Берег1,Берег2),  
write("Повезли волка на"),write(Берег2),nl,  
состояние(Берег2,Берег2,Коза,Капуста,волк).  
  
  
состояние(Берег1,Волк,Берег1,Капуста,Последний):-  
Последний\==коза,напротив(Берег1,Берег2),  
write("Повезли козу на "),write(Берег2),nl,  
состояние(Берег2,Волк,Берег2,Капуста,коза).  
  
  
состояние(Берег1,Волк,Коза,Берег1,Последний):-  
Последний\==капуста,напротив(Берег1,Берег2),  
write("Повезли капусту на "),write(Берег2),nl,  
состояние(Берег2,Волк,Коза,Берег2,капуста).  
  
напротив(правый,левый).  
  
напротив(левый,правый).  
  
задача:-состояние(левый,левый,левый,левый,ничего).  
```